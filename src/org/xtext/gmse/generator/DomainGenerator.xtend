/*
 * generated by Xtext 2.18.0
 */
package org.xtext.gmse.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import org.xtext.gmse.domain.Festival;
import org.xtext.gmse.domain.Artist;
import org.xtext.gmse.domain.Stage;
import org.xtext.gmse.domain.TimetableEntry;
import org.xtext.gmse.domain.Food;
import org.eclipse.xtext.naming.IQualifiedNameProvider;

import com.google.inject.Inject

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class DomainGenerator extends AbstractGenerator {
	
	@Inject extension IQualifiedNameProvider 

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		for (f : resource.allContents.toIterable.filter(Festival))
		fsa.generateFile(f.fullyQualifiedName.toString("/")+".html",
			f.compile
		)

	}

	
	def compile(Festival f)'''
		
		<html>
			<head>
				<title>«f.name»</title>
			</head>
			<body>
				<div class="information">
					<h1 class="info">Allgemeine Informationen</h1>
					<h2 class="info_festivalname">«f.name»</h2>
					<p class="info_visitors">«f.visitors» Besucher</p>
					<p class="info_start_date">von: «f.startdate»</p>
					<p class="info_end_date">bis: «f.enddate»</p>
				</div>
				<div class="lineup">
				<h1>Lineup</h1>
					<ul>
					«FOR l : f.lineup.artists»
					«l.compile»
					«ENDFOR»
					</ul>
				</div>
				<div class="stages">
					<h1>Stages</h1>
					«FOR s : f.stages.stages»
					«s.compile»
					«ENDFOR»
				</div>
				<div class="timetable">
				<h1>Timetable</h1>
				«FOR t : f.timetable.timetable»	
				«t.compile»
				«ENDFOR»
				</div>
				<div class="food">
				<h1>Food Lineup</h1>
				«FOR fo : f.foodLineup.foodlist»	
				«fo.compile»
				«ENDFOR»
				</div>	
			</body>
		</html>
	'''
	
	def compile(Artist a)'''
		<li class="artist">«a.name» («a.country»)</li>
	'''
	
	def compile(Stage s)'''
		<div class="stage">
			<h2 class="name">«s.name»</h2>
			<p class="capacity">«s.capacity»</p>
			<p class="info">«s.info»</p>
		</div>
	'''	
	
	def compile(TimetableEntry t)'''
		<div class="act">
			<h2 class="artist_name">«t.name.getName()»</h2>
			<p class="day">«t.day»</p>
			<p class="timeslot">«t.time»</p>
			<p class="stage">«t.stage.getName()»</p>
		</div>
	'''
	
	def compile(Food fo)'''
		<div class="food">
			<h3 class="food_name">«fo.name»</h3>
			<p class="typ">«fo.type»</p>
			<p class="place">«fo.place»</p>
		</div>
	'''
	
}	
